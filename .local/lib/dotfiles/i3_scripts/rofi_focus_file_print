#!/usr/bin/awk -f
function deque_push(id) {
	if (id != deque_head) {
		if (deque[id, deque_left]) {
			if (deque[id, deque_left] != -1)
				deque[deque[id, deque_left], deque_right] = deque[id, deque_right]
			if (deque[id, deque_right] != -1)
				deque[deque[id, deque_right], deque_left] = deque[id, deque_left]
		}
		if (deque_head == -1) deque_tail = id
		else if (deque_tail == id) deque_tail = deque[id, deque_left]
		deque[id, deque_left] = -1
		deque[id, deque_right] = deque_head
		deque[deque_head, deque_left] = id
		deque_head = id
	}
}
function deque_push_tail(id) {
	if (id != deque_tail) {
		if (deque[id, deque_left]) {
			if (deque[id, deque_left] != -1)
				deque[deque[id, deque_left], deque_right] = deque[id, deque_right]
			if (deque[id, deque_right] != -1)
				deque[deque[id, deque_right], deque_left] = deque[id, deque_left]
		}
		if (deque_tail == -1) deque_head = id
		else if (deque_head == id) deque_head = deque[id, deque_right]
		deque[id, deque_left] = deque_tail
		deque[id, deque_right] = -1
		deque[deque_tail, deque_right] = id
		deque_tail = id
	}
}
BEGIN {
	deque_head = deque_tail = -1; deque_left = 0; deque_right = 1
	stack_file = "/tmp/rofi_focus_file_stack." ENVIRON["USER"]
	aliases[ENVIRON["USER"]] = "~"
	aliases["Downloads"] = "dl"
	aliases["onedrive"] = "ondr"
	aliases["school_onedrive"] = "sond"
	prev = -1
	while ((code = (getline cid < stack_file)) > 0) {
		cache[prev] = cid
		prev = cid
	}
	close(stack_file)
	if (code == -1) update_cache = 1
}
$2 == "-1" && $3 == "caja.Caja" && $1 != ignore {
	id = $1
	$1=$2=$3=$4=""
	title = substr($0,5)
	if (title in aliases) {
		title = aliases[title] ":" title
		delete aliases[title]
	}
	out[id] = title
	deque_push(id)
	if (!update_cache && !cache[id] && id != cid) update_cache = 1
}
END {
	if (deque_head == -1) exit 1
	prev = -1
	while ((prev = cache[prev]))
		if (out[prev]) deque_push_tail(prev)
	printf("%cprompt%cfolder/file\n", 0, 31)
	printf("%cno-custom%ctrue\n", 0, 31)
	printf("%cuse-hot-keys%ctrue\n", 0, 31)
	if (update_cache) printf("") > stack_file
	while (deque_head != -1) {
		printf("%s%cinfo%c%s\n", out[deque_head], 0, 31, deque_head)
		if (update_cache) print deque_head >> stack_file
		deque_head = deque[deque_head, deque_right]
	}
	if (update_cache) {
		close(stack_file)
		if (code == -1) {
			chmod = "chmod 600 '" stack_file "'"
			system(chmod)
			close(chmod)
		}
	}
}
